apply plugin: 'com.android.application'

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}

repositories {
    jcenter()
}

android {
    lintOptions {
        lintConfig file("lint.xml")
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        versionCode 381
        versionName "4.8.1"
        minSdkVersion 9
        targetSdkVersion 23
    }

    signingConfigs {
        release
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
            signingConfig signingConfigs.release
        }
        // https://code.google.com/p/android/issues/detail?id=123771
        debuggable.initWith(buildTypes.debug)
        debuggable {
            testCoverageEnabled false
        }
    }

    if (project.hasProperty('keyAlias') &&
            project.hasProperty('storeFile') &&
            project.hasProperty('storePassword') &&
            project.hasProperty('keyPassword')) {
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        buildTypes.release.signingConfig = null
    }

    productFlavors {
        generic
        googleplay
    }
}

dependencies {
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.squareup.dagger:dagger:1.2.2'

    compile 'com.android.support:design:23.1.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:4.1.0'
    compile 'com.google.guava:guava-jdk5:17.0'
    compile ('com.rubiconproject.oss:jchronic:0.2.6') {
        transitive = false
    }
    compile ('org.scala-saddle:google-rfc-2445:20110304') {
        transitive = false
    }
    compile ('com.github.flavienlaurent.datetimepicker:library:0.0.2') {
        exclude group: 'com.android.support', module: 'support-v4'
    }

    googleplayCompile 'com.google.android.gms:play-services-location:8.4.0'
    googleplayCompile 'com.google.android.gms:play-services-analytics:8.4.0'
    googleplayCompile('com.google.apis:google-api-services-tasks:v1-rev41-1.21.0') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    googleplayCompile('com.google.api-client:google-api-client-android:1.21.0') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    androidTestCompile ('org.mockito:mockito-core:1.9.5') {
        transitive = false
    }
    androidTestCompile 'com.google.dexmaker:dexmaker:1.1'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.1'
    androidTestCompile ('com.natpryce:make-it-easy:3.1.0') {
        transitive = false
    }
}
