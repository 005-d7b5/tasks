apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.neenbedankt.android-apt'

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.5.2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

repositories {
    jcenter()
}

android {
    lintOptions {
        lintConfig file("lint.xml")
    }

    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "org.tasks"
        versionCode 408
        versionName "4.8.17"
        minSdkVersion 14
        targetSdkVersion 24

//        jackOptions {
//            enabled true
//        }
    }

    signingConfigs {
        release
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
//        sourceCompatibility JavaVersion.VERSION_1_8
//        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
            signingConfig signingConfigs.release
        }
    }

    if (project.hasProperty('keyAlias') &&
            project.hasProperty('storeFile') &&
            project.hasProperty('storePassword') &&
            project.hasProperty('keyPassword')) {
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        buildTypes.release.signingConfig = null
    }

    productFlavors {
        generic
        googleplay
        amazon
    }
}

configurations {
    all*.exclude group: 'com.google.guava', module: 'guava-jdk5'
    all*.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
}

dependencies {
    apt 'com.google.dagger:dagger-compiler:2.4'
    compile 'com.google.dagger:dagger:2.4'

    apt 'com.jakewharton:butterknife-compiler:8.1.0'
    compile 'com.jakewharton:butterknife:8.1.0'

    compile 'com.nononsenseapps:filepicker:2.5.2'
    compile 'com.android.support:design:24.0.0'
    compile 'com.android.support:support-v13:24.0.0'
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.google.guava:guava:19.0'
    compile ('com.rubiconproject.oss:jchronic:0.2.6') {
        transitive = false
    }
    compile ('org.scala-saddle:google-rfc-2445:20110304') {
        transitive = false
    }
    compile ('com.wdullaer:materialdatetimepicker:2.3.0') {
        exclude group: 'com.android.support', module: 'support-v4'
    }

    googleplayCompile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    googleplayCompile 'com.twofortyfouram:android-plugin-api-for-locale:[1.0.1,2.0['
    googleplayCompile 'com.google.android.gms:play-services-location:9.0.2'
    googleplayCompile 'com.google.android.gms:play-services-analytics:9.0.2'
    googleplayCompile 'com.google.android.gms:play-services-auth:9.0.2'
    googleplayCompile 'com.google.apis:google-api-services-tasks:v1-rev45-1.22.0'
    googleplayCompile 'com.google.api-client:google-api-client-android:1.22.0'

    amazonCompile 'com.google.android.gms:play-services-analytics:9.0.2'

    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'com.natpryce:make-it-easy:4.0.0'
}
